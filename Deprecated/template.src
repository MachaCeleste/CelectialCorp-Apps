metaxploit = include_lib("/lib/metaxploit.so")
if not metaxploit then metaxploit = include_lib(current_path + "/metaxploit.so")
if not metaxploit then exit("Error: Can't find metaxploit library in the /lib path or the current folder")

print("\n<align=center>Celestial Tools" + char(169) + "</align>\n<align=center>Template</align>\n")

mem = user_input("put memory address here: ")
unsecstr = user_input("put unsecured string here: ")

//LinQ v2 lib functionality for greyscript
//By MachaCeleste 20250210
Linq = function(e,k,v)
    if typeof(e)=="file" then
        if k=="namehas" then
            if e.name.is_match(v) then return true
        end if
        if k=="has_permission" then
            s=v.values
            c=""
            for p in s
                if e.has_permission(p) then c=c+p
            end for
            if c==v then return true
            return false
        end if
    end if
    if e isa string then
        if k=="is" and e==v then return true
        if k=="contains" and e.is_match(v) then return true
        return false
    end if
    if not e.values[0].hasIndex(k) then return print("Index " + k + " not found in map!")
    f = @e[k]
    if f(e)==v then return true
    return false
    end function
list.first = function(k,v)
    for e in self
        if Linq(e,k,v) then return e
    end for
    return null
    end function
list.where = function(k,v)
    r = []
    for e in self
        if Linq(e,k,v) then r.push(e)
    end for
    if r.len>0 then return r
    return null
    end function
list.wherenot = function(k,v)
    r = []
    for e in self
        if not Linq(e,k,v) then r.push(e)
    end for
    if r.len>0 then return r
    end function

toRoot = function(f)
    while parent(f)
        f=parent(f)
    end while
    return f
end function

if params.len != 2 or params[0] == "-h" or params[0] == "--help" then exit("<b>Usage: "+program_path.split("/")[-1]+" [ip_address] [port]</b>")
address = params[0]
port = params[1].to_int
net_session = metaxploit.net_use(address, port)
if not net_session then exit("Error: Couldn't establish net session")
metaLib = net_session.dump_lib
print("Grabbed " + metaLib.lib_name + " v" + metaLib.version)
res = metaLib.overflow(mem, unsecstr)
if typeof(res) == "number" then exit("Firewall disabled!")
if not res then res = metaLib.overflow(mem, unsecstr, "password")
if typeof(res) == "number" then exit("Password changed to ""password"" for above user!")
if not res then res = metaLib.overflow(mem, unsecstr, user_input("Enter target LanIp: "))
if not res then exit("Error: res returned null")
print("Gained " + typeof(res))

getUsrInfo = function(userfolders)
out = "Account:Password\n"
for userfolder in userfolders.wherenot("name", "guest")
    config = userfolder.get_folders.first("name", "Config")
    mail = config.get_files.first("name", "Mail.txt")
    if mail then out = out + mail.get_content + char(10)
    bank = config.get_files.first("name", "Bank.txt")
    if bank then out = out + bank.get_content + char(10)
end for
if out == "Account:Password\n" then return null
return out
end function

if typeof(res) == "shell" then
    res.start_terminal
    return

else if typeof(res) == "computer" then
    file = res.File("/etc/passwd")
    if file then print("passwd:\n" + file.get_content)
    file = res.File("/home/")
    userfolders = file.get_folders
    if not userfolders.len > 1 then exit("Error: no users found!")
    out = getUsrInfo(userfolders)
    if not out then exit("Error: no user data found!")
    print("User Accounts:\n" + format_columns(out))
    return
else if typeof(res) == "file" then
    file = toRoot(res).get_folders.first("name", "etc").get_files.first("name", "passwd")
    if file.has_permission("r") then print("passwd:\n" + file.get_content)
    file = toRoot(res).get_folders.first("name", "home")
    userfolders = file.get_folders
    if not userfolders.len > 1 then exit("Error: no users found!")
    out = getUsrInfo(userfolders)
    if not out then exit("Error: no user data found!")
    print("User Accounts:\n" + format_columns(out))
    return
else
    print("Error: ret = null")
end if